// Effect Syntax Guide: https://docs.cocos.com/creator/manual/zh/shader/index.html

CCEffect %{
  techniques:
  - name: opaque
    passes:
    - vert: unlit-vs:vert
      frag: unlit-fs:frag
      properties: &props
        dissolveTexture:    { value: white }
        amount:      { value: 0.0, editor: {range:[0, 2, 0.01], slide:  true } }
        dissolveScale:      { value: 0.0, editor: {range:[0, 10, 0.01], slide:  true } }
        lineSize:      { value: 0.0, editor: {range:[0, 1, 0.01], slide:  true } }
        lineColor:      { value: [1.0, 1.0, 1.0, 1.0], editor: { type: color } }
        lightDirection:    { value: [0.25, 1.0, -0.25, 1.0] }
        brightness:      { value: 0.5, editor: {range:[0, 1, 0.01], slide:  true } }
        shadow:      { value: 0.5, editor: {range:[0, 1, 0.01], slide:  true } }
        mainTexture:    { value: white }
        mainTiling:    { value: [1.0, 1.0] }
        mainOffset:    { value: [0.0, 0.0] }
        mainColor:      { value: [1.0, 1.0, 1.0, 1.0], editor: { type: color } }
  - name: transparent
    passes:
    - vert: unlit-vs:vert
      frag: unlit-fs:frag
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendSrcAlpha: src_alpha
          blendDstAlpha: one_minus_src_alpha
      properties: *props
}%

CCProgram unlit-vs %{
  precision highp float;
  #include <legacy/input-standard>
  #include <builtin/uniforms/cc-global>
  #include <legacy/local-batch>
  #include <legacy/input-standard>
  #include <legacy/fog-vs>
  #include <legacy/shadow-map-vs>
  
  in vec4 a_color;
  
  out vec3 v_position;
  out vec3 v_normal;
  out vec3 v_tangent;
  out vec3 v_bitangent;
  out vec2 v_uv_main;
  out vec4 v_color;

  vec4 vert () {
    StandardVertInput In;
    CCVertInput(In);

    mat4 matWorld, matWorldIT;
    CCGetWorldMatrixFull(matWorld, matWorldIT);

    vec4 pos = matWorld * In.position;

    v_position = pos.xyz;
    v_normal = normalize((matWorldIT * vec4(In.normal, 0.0)).xyz);
    v_tangent = normalize((matWorld * vec4(In.tangent.xyz, 0.0)).xyz);
    v_bitangent = cross(v_normal, v_tangent) * In.tangent.w;

    v_uv_main = a_texCoord;
    v_color = a_color;

    CC_TRANSFER_FOG(pos);
    CC_TRANSFER_SHADOW(pos);

    return cc_matProj * (cc_matView * matWorld) * In.position;
  }
}%

CCProgram unlit-fs %{
  precision highp float;
  #include <legacy/output>
  #include <legacy/fog-fs>

  in vec2 v_uv_main;
  in vec3 v_position;
  in vec3 v_normal;

  uniform sampler2D mainTexture;
  uniform sampler2D dissolveTexture;

  uniform Constant {
    vec2 mainTiling;
    vec2 mainOffset;
    vec4 mainColor;
    vec4 lineColor;
    vec4 lightDirection;
    float brightness;
    float shadow;
    float amount;
    float dissolveScale;
    float lineSize;
  };

  float lerp(float a, float b, float t) { return (1.0 - t) * a + t * b; }

  vec4 frag () {
    vec2 uv_main = v_uv_main * mainTiling.xy + mainOffset.xy;
    vec4 col = mainColor * texture(mainTexture, uv_main);

    vec2 uv_front = v_position.xy * mainTiling.xy + mainOffset.xy;
    vec2 uv_side = v_position.zy * mainTiling.xy + mainOffset.xy;
    vec2 uv_top = v_position.xz * mainTiling.xy + mainOffset.xy;
    
    vec4 dissolve_col_front = texture(dissolveTexture, fract(uv_front * dissolveScale));
    vec4 dissolve_col_side = texture(dissolveTexture, fract(uv_side * dissolveScale));
    vec4 dissolve_col_top = texture(dissolveTexture, fract(uv_top * dissolveScale));

    vec3 weights = v_normal;
    weights = abs(weights);

    dissolve_col_front *= weights.z;
    dissolve_col_side *= weights.x;
    dissolve_col_top *= weights.y;

    vec4 disolveColor = dissolve_col_front + dissolve_col_side + dissolve_col_top;
    float dissolve_value = disolveColor.r;

    if ((dissolve_value - amount) < 0.0) {
      discard;
    }

    if (dissolve_value > amount)
    {
      if (dissolve_value - lineSize < amount)
      {
        col = lineColor;
      }
    }
    
    vec3 th = normalize(vec3(lightDirection.x, lightDirection.y, lightDirection.z));
    float lightVal = dot (v_normal, th);
    col.rgb = col.rgb * vec3(mainColor.r + lightVal * brightness, mainColor.g + lightVal * brightness, mainColor.b + lightVal * brightness);

    float maximum = max(lerp(1.5, 0.0, shadow), lightVal) + lightVal * brightness;

    col.rgb = col.rgb * vec3(mainColor.r * maximum, mainColor.g * maximum, mainColor.b * maximum);
    
    CC_APPLY_FOG(col, v_position);
    return CCFragOutput(col);
  }
}%
